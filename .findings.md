# `[H-01]` Bridging $NATI token amounts lesser than 1e14 will cause tokens to be lost and stuck in `GNATI_BRIDGE`

## Summary

The `GNATI_BRIDGE::swapToBridge()` function is used to bridge $NATI tokens from the Ethereum mainnet to the Verus chain. However, it lacks checks to see if the calculated amount to bridge is equal to zero or not, which can lead to token loss for the user.

## Vulnerability Details

The `GNATI_BRIDGE` is an intermediate proxy which reduces the total market cap of the $NATI tokens from 33 trillion (33e30, with 18 decimals) to 3.3 billion (33e26 with 18 decimals). Thus, any tokens that need to be bridged are first scaled down by 1e4. All coins on Verus use 8 decimals, so further scaling down from 18 decimals to 8 decimals takes place by dividing by 1e10. This can be seen from the code segment below,

```javascript
    function swapToBridge(
        uint256 _amountToSwap,
        address addressTo,
        uint8 addressType,
        address bridgeAddress,
        address destinationCurrency,
        address feecurrencyid
    ) public payable {
        // more code here

        // amount to mint of proxy token that only the bridge accepts
@>      uint256 amountToMint = _amountToSwap / multiplier;

        // more code here

@>      uint64 verusAmount = uint64(amountToMint / SATS_TO_WEI_STD); // from 18 decimals to 8

        VerusBridge(bridgeAddress).sendTransfer{value: msg.value}(
            buildReserveTransfer(verusAmount, addressTo, addressType, destinationCurrency, feecurrencyid)
        );
    }
```

However, while bridging the tokens there is no check to see if the `verusAmount` is 0 or not. If the amount to bridge is less than 1e14 (and has 4 trailing zeros to bypass the `_amountToSwap % multiplier == 0` check), then the tokens will be locked up in the proxy contract forever, unable to be recovered. This is because the Verus Ethereum Bridge does not revert for 0 amount transfers.

## Impact

If any amount smaller than 1e14 is bridged, tokens will be stuck in the `GNATI_BRIDGE` proxy forever.

## Proof of Concept

The following fork test (Ethereum Sepolia, with Foundry) demonstrates how tokens can be lost.

<details>
<summary>Code</summary>

```javascript
contract ProxyTest is Test {
    struct ProxyConfig {
        address linkedERC20;
        address thisTokeniAddress;
        address destinationCurrency;
        address vETHiAddress;
    }

    ProxyConfig public s_config;
    address public s_proxy;
    address public s_verusBridge;
    address public s_natiWhale;
    address public s_receiver;

    uint256 private s_sepoliaFork;
    uint256 private s_rollTo;

    function setUp() public {
        s_config = ProxyConfig({
            linkedERC20: 0xA23DFcF889e9544fa8d7DC8e3774b979F4Ca5bA1,
            thisTokeniAddress: 0xE73b92E469c49A4651AeFf204ecFE920a78022DB,
            destinationCurrency: 0xffEce948b8A38bBcC813411D2597f7f8485a0689,
            vETHiAddress: 0x67460C2f56774eD27EeB8685f29f6CEC0B090B00
        });

        s_proxy = 0xc363C4eda3bEF13984F1E170a6840E0d8Bc777aA;
        s_verusBridge = 0xCaA98A4eC79dAC8A06Cb3BfDcF5351b6576d939f;
        s_natiWhale = 0x4a7C219FB111982C81Ce777F2edBD692663e0A34;

        s_rollTo = 6304433;
        s_receiver = 0x55F51a22c79018A00CEd41e758560F5dF7d4d35d;

        string memory SEPOLIA_RPC_URL = vm.envString("SEPOLIA_RPC_URL");
        s_sepoliaFork = vm.createSelectFork(SEPOLIA_RPC_URL);
        vm.rollFork(s_rollTo);
    }

    function testBridgingSmallAmountsLeadsToTokenLoss() public {
        uint256 dealAmount = 0.003 ether;
        deal(s_natiWhale, dealAmount);

        uint8 addresstype = 2;
        uint256 amountToBridge = 5e13;

        uint256 bridgeProxyTokenBalanceBefore = IERC20(s_proxy).balanceOf(s_verusBridge);
        uint256 whaleNatiBalanceBefore = IERC20(s_config.linkedERC20).balanceOf(s_natiWhale);
        uint256 proxyNatiTokenBalanceBefore = IERC20(s_config.linkedERC20).balanceOf(s_proxy);

        vm.startPrank(s_natiWhale);
        IERC20(s_config.linkedERC20).approve(s_proxy, amountToBridge);
        IProxy(s_proxy).swapToBridge{value: dealAmount}(
            amountToBridge, s_receiver, addresstype, s_verusBridge, s_config.destinationCurrency, s_config.vETHiAddress
        );

        uint256 bridgeProxyTokenBalanceAfter = IERC20(s_proxy).balanceOf(s_verusBridge);
        uint256 whaleNatiBalanceAfter = IERC20(s_config.linkedERC20).balanceOf(s_natiWhale);
        uint256 proxyNatiTokenBalanceAfter = IERC20(s_config.linkedERC20).balanceOf(s_proxy);

        assertEq(bridgeProxyTokenBalanceBefore, bridgeProxyTokenBalanceAfter);
        assertEq(whaleNatiBalanceBefore - whaleNatiBalanceAfter, amountToBridge);

        console.log("The proxy tokens held by the verus bridge contract before: ", bridgeProxyTokenBalanceBefore);
        console.log("The proxy tokens held by the verus bridge contract after: ", bridgeProxyTokenBalanceAfter);
        console.log("The NATI tokens held by user before bridging: ", whaleNatiBalanceBefore);
        console.log("The NATI tokens held by user after bridging: ", whaleNatiBalanceAfter);
        console.log("Tokens stuck in the proxy: ", proxyNatiTokenBalanceAfter - proxyNatiTokenBalanceBefore);
    }
}
```

</details>

<br />

The test passes with the following logs,

```bash
Ran 1 test for test/Proxy.t.sol:ProxyTest
[PASS] testBridgingSmallAmountsLeadsToTokenLoss() (gas: 617452)
Logs:
  The proxy tokens held by the verus bridge contract before:  32000500000000000000
  The proxy tokens held by the verus bridge contract after:   32000500000000000000
  The NATI tokens held by user before bridging:               99999669996000000000000000000
  The NATI tokens held by user after bridging:                99999669995999950000000000000
  Tokens stuck in the proxy:                                  50000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 5.67s (6.11ms CPU time)
```

## Recommended Mitigation

Add a check in the `GNATI_BRIDGE` contract to see if the amount to bridge is greater than zero or not,

```diff
    function swapToBridge(
        uint256 _amountToSwap,
        address addressTo,
        uint8 addressType,
        address bridgeAddress,
        address destinationCurrency,
        address feecurrencyid
    ) public payable {
        
        // more code here
        
        // amount to mint of proxy token that only the bridge accepts
        uint256 amountToMint = _amountToSwap / multiplier;

        // more code here

        uint64 verusAmount = uint64(amountToMint / SATS_TO_WEI_STD); // from 18 decimals to 8

+       require(amountToMint > 0 && verusAmount > 0, "Insufficient amount to bridge");        

        VerusBridge(bridgeAddress).sendTransfer{value: msg.value}(buildReserveTransfer(verusAmount, addressTo, addressType, destinationCurrency, feecurrencyid));
    }
```


# `[H-02]` $NATI tokens can be lost and stuck in the contract due to rounding down while dividing

## Summary

The `GNATI_BRIDGE::swapToBridge()` function has an initial check to see if the amount to bridge does not undergo truncation while dividing by 1e4, which is to scale down the amount to go from a 33 trillion to a 3.3 billion market cap. However, truncation due to rounding down is also possible while adjusting the amount to 8 decimals (for Verus) by dividing by 1e10. This can lead to some tokens being stuck in `GNATI_BRIDGE`.

## Vulnerability Details

Consider the following code segment from `GNATI_BRIDGE::swapToBridge()`,

```javascript
    function swapToBridge(
        uint256 _amountToSwap,
        address addressTo,
        uint8 addressType,
        address bridgeAddress,
        address destinationCurrency,
        address feecurrencyid
    ) public payable {
        // more code here

        // make sure amount being sent is a multiple of the multiplier to stop wei being lost in truncation
@>      require(_amountToSwap % multiplier == 0, "not divisable by 1000000");

        // more code here

@>      uint64 verusAmount = uint64(amountToMint / SATS_TO_WEI_STD); // from 18 decimals to 8

        VerusBridge(bridgeAddress).sendTransfer{value: msg.value}(
            buildReserveTransfer(verusAmount, addressTo, addressType, destinationCurrency, feecurrencyid)
        );
    }
```

The first check can be bypassed with any amount with 4 trailing zeros (multiplier value is 1e4). However, lack of such a check for the 14 trailing zeros implies that any amount upto 1e14 can be lost and stuck in `GNATI_BRIDGE` while bridging. 

## Impact

While bridging tokens, it is possible that any amount upto 1e14 can be lost.

## Proof of Concept

Consider a user who wants to bridge 1000050000000000000 $NATI tokens to the Verus chain.

1. The `GNATI_BRIDGE` transfers 1000050000000000000 $NATI from the user to itself.
2. The amount is first scaled down by 1e4, leaving us with 100005000000000.
3. Now, to convert the amount to 8 decimals, we divide by 1e10. This leaves us with 10000 tokens.
4. 1e4 tokens will be brideged over to Verus. However, while bridging back to Ethereum mainnet, the user will only be able to claim 1000000000000000000 $NATI (scaling up by 1e14).
5. The amount of tokens lost by user while bridging is 1000050000000000000 - 1000000000000000000 = 5e13.

Here's a fork test (Ethereum Sepolia, with Foundry),

<details>
<summary>Code</summary>

```javascript
contract ProxyTest is Test {
    struct ProxyConfig {
        address linkedERC20;
        address thisTokeniAddress;
        address destinationCurrency;
        address vETHiAddress;
    }

    ProxyConfig public s_config;
    address public s_proxy;
    address public s_verusBridge;
    address public s_natiWhale;
    address public s_receiver;

    uint256 private s_sepoliaFork;
    uint256 private s_rollTo;

    function setUp() public {
        s_config = ProxyConfig({
            linkedERC20: 0xA23DFcF889e9544fa8d7DC8e3774b979F4Ca5bA1,
            thisTokeniAddress: 0xE73b92E469c49A4651AeFf204ecFE920a78022DB,
            destinationCurrency: 0xffEce948b8A38bBcC813411D2597f7f8485a0689,
            vETHiAddress: 0x67460C2f56774eD27EeB8685f29f6CEC0B090B00
        });

        s_proxy = 0xc363C4eda3bEF13984F1E170a6840E0d8Bc777aA;
        s_verusBridge = 0xCaA98A4eC79dAC8A06Cb3BfDcF5351b6576d939f;
        s_natiWhale = 0x4a7C219FB111982C81Ce777F2edBD692663e0A34;

        s_rollTo = 6304433;
        s_receiver = 0x55F51a22c79018A00CEd41e758560F5dF7d4d35d;

        string memory SEPOLIA_RPC_URL = vm.envString("SEPOLIA_RPC_URL");
        s_sepoliaFork = vm.createSelectFork(SEPOLIA_RPC_URL);
        vm.rollFork(s_rollTo);
    }

    function testRoundingDownCausesTokensToBeStuck() public {
        uint256 dealAmount = 0.003 ether;
        deal(s_natiWhale, dealAmount);

        uint8 addresstype = 2;
        uint256 amountToBridge = 1000050000000000000;

        uint256 bridgeProxyTokenBalanceBefore = IERC20(s_proxy).balanceOf(s_verusBridge);
        uint256 proxyNatiTokenBalanceBefore = IERC20(s_config.linkedERC20).balanceOf(s_proxy);

        vm.startPrank(s_natiWhale);
        IERC20(s_config.linkedERC20).approve(s_proxy, amountToBridge);
        IProxy(s_proxy).swapToBridge{value: dealAmount}(
            amountToBridge, s_receiver, addresstype, s_verusBridge, s_config.destinationCurrency, s_config.vETHiAddress
        );

        uint256 bridgeProxyTokenBalanceAfter = IERC20(s_proxy).balanceOf(s_verusBridge);
        uint256 proxyNatiTokenBalanceAfter = IERC20(s_config.linkedERC20).balanceOf(s_proxy);

        uint256 tokensLost = (proxyNatiTokenBalanceAfter - proxyNatiTokenBalanceBefore)
            - (bridgeProxyTokenBalanceAfter - bridgeProxyTokenBalanceBefore) * 1e4;
        console.log("The amount of $NATI tokens stuck in the contract and cannot be recovered: ", tokensLost);
    }
}
```

</details>
<br />

The test passes with the following logs,

```shell
Ran 1 test for test/Proxy.t.sol:ProxyTest
[PASS] testRoundingDownCausesTokensToBeStuck() (gas: 617367)
Logs:
  The amount of $NATI tokens stuck in the contract and cannot be recovered:  50000000000000

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 6.74s (5.09ms CPU time)

Ran 1 test suite in 6.74s (6.74s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

## Recommended Mitigation

Make the following changes in `GNATI_BRIDGE::swapToBridge()` function,

```diff
    function swapToBridge(
        uint256 _amountToSwap,
        address addressTo,
        uint8 addressType,
        address bridgeAddress,
        address destinationCurrency,
        address feecurrencyid
    ) public payable {
        // more code here

        // make sure amount being sent is a multiple of the multiplier to stop wei being lost in truncation
-       require(_amountToSwap % multiplier == 0, "not divisable by 1000000");
+       require(_amountToSwap % (multiplier * SATS_TO_WEI_STD) == 0, "Not divisible by 1e14");

        // more code here
    }
```

This will ensure that users will bridge an amount that never undergoes truncation due to division, first by 1e4, and then by 1e10.


# `[L-01]` Missing event emission in `GNATI_BRIDGE::transfer()` and `GNATI_BRIDGE::swapToBridge()` functions

## Vulnerability Details

The `GNATI_BRIDGE` lacks event emission in two major functions `GNATI_BRIDGE::transfer()` and `GNATI_BRIDGE::swapToBridge()`. The former is used to bridge tokens from Verus to Ethereum, and the latter bridges tokens in the opposite direction.

## Impact

Events are used by off-chain services to track on-chain activities. It is also useful for debugging purposes.

## Recommended Mitigation

Make the following changes in `GNATI_BRIDGE`,

```diff
contract GNATI_BRIDGE is ERC20 {
    // more code here

+   event Bridged(bool fromEthereumToVerus, uint256 amountFrom, uint256 amountTo, address receiver);

    // more code here

    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        _burn(msg.sender, amount);

        //send the scaled up amount back to the user on ETH
        ERC20(linkedERC20).transfer(to, (amount * multiplier));

+       emit Bridged(false, amount, (amount * multiplier), to);

        return true;
    }

    function swapToBridge(
        uint256 _amountToSwap,
        address addressTo,
        uint8 addressType,
        address bridgeAddress,
        address destinationCurrency,
        address feecurrencyid
    ) public payable {
        
        // more code here

        uint64 verusAmount = uint64(amountToMint / SATS_TO_WEI_STD); // from 18 decimals to 8

        VerusBridge(bridgeAddress).sendTransfer{value: msg.value}(
            buildReserveTransfer(verusAmount, addressTo, addressType, destinationCurrency, feecurrencyid)
        );

+       emit Bridged(true, _amountToSwap, verusAmount, addressTo);
    }

    // more code here

}
```


# `[L-02]` Allowing user supplied bridge address in `GNATI_BRIDGE` can cause anomalies, breaking protocol invariant

## Summary

With an arbitrary bridge address supplied by users, they can get hold of proxy tokens without actually bridging them to Verus. Also, a lack of bridge address implies a lack of access control for `GNATI_BRIDGE::transfer()` function.

## Vulnerability Details

The `GNATI_BRIDGE::swapToBridge()` function allows the user to supply an arbitrary bridge address. Users can pass a custom contract address they own which has a `sendTransfer()` function. This will allow them to transfer the proxy tokens to themselves without actually bridging $NATI tokens to Verus, breaking the core invariant -- proxy tokens should only be held by the bridge, and a scaled down amount should be freed up on Verus.

Additionally, the `GNATI_BRIDGE::transfer()` function should be used exclusively by the Verus Ethereum bridge to transfer a scaled up amount to a receiver once $NATI tokens are bridged from Verus to Ethereum. Users should not hold the proxy tokens directly, and thus should not be exposed to the transfer function.

## Impact

Users can pass a custom contract address they own which has a `sendTransfer()` function, and get hold of proxy tokens without actually bridging to Verus. This breaks two protocol invariants:

1. Proxy tokens should only be held by the Verus Ethereum bridge.
2. If proxy tokens are minted on Ethereum, then a scaled down amount should be freed up and in circulation on Verus.

Additionally, since the `GNATI_BRIDGE::transfer()` function lacks access control, an attacker can drain the bridge if they can find a way to mint more tokens.

## Recommended Mitigation

Do not allow users to supply an arbitrary bridge address. Set a `verusBridge` address variable in `GNATI_BRIDGE` and direct calls to it. Since the bridge address is susceptible to change in the future (confirmed by the Verus community), set a trusted admin for the `GNATI_BRIDGE` contract who can change the `verusBridge` address as required.

Also, add access control to the `GNATI_BRIDGE::transfer()` function so that it is only callable by the Verus Ethereum bridge.


# `[I-01]` Use `safeTransfer()` instead of `transfer()` function in `GNATI_BRIDGE::transfer()` function

The `GNATI_BRIDGE::transfer()` function sends the scaled up $NATI tokens to the receiver on bridging from Verus to Ethereum using the `IERC20::transfer()` function. It is recommended that `safeTransfer()` from Openzeppelin's `SafeERC20` library is used to ensure token transfers do not fail silently.


# `[I-02]` Avoid using magic numbers in `GNATI_BRIDGE::swapToBridge()`

Magic number is used for the ETH amount to be supplied for bridging tokens, as can be seen below,

```javascript
    function swapToBridge(
        uint256 _amountToSwap,
        address addressTo,
        uint8 addressType,
        address bridgeAddress,
        address destinationCurrency,
        address feecurrencyid
    ) public payable {
@>      require(msg.value == 0.003 ether, "0.003 ETH required");

        // more code here

    }
```

Avoid doing this. Always use constants. Make the following changes,

```diff
contract GNATI_BRIDGE is ERC20 {
    
    // more code here

+   uint256 public constant fee = 0.003 ether;

    function swapToBridge(
        uint256 _amountToSwap,
        address addressTo,
        uint8 addressType,
        address bridgeAddress,
        address destinationCurrency,
        address feecurrencyid
    ) public payable {
-       require(msg.value == 0.003 ether, "0.003 ETH required");
+       require(msg.value == fee, "0.003 ETH required");

        // more code here
    
    }
}
```


# `[G-01]` Use external functions instead of public functions to save gas

The `GNATI_BRIDGE::transfer()` and `GNATI_BRIDGE::swapToBridge()` functions should be marked with external visibility to save gas.


# `[G-02]` Use custom errors instead of `require` statements in `GNATI_BRIDGE::swapToBridge()`

Custom errors use a lot less gas than require statements. Make the following changes,

```diff
contract GNATI_BRIDGE is ERC20 {
    
    // more code here

+   event InsufficientFeesSupplied();
+   event IncorrectInputAmount();

    function swapToBridge(
        uint256 _amountToSwap,
        address addressTo,
        uint8 addressType,
        address bridgeAddress,
        address destinationCurrency,
        address feecurrencyid
    ) public payable {
-       require(msg.value == 0.003 ether, "0.003 ETH required");
+       if (msg.value != 0.003 ether) revert InsufficientFeesSupplied();

        // make sure amount being sent is a multiple of the multiplier to stop wei being lost in truncation
-       require(_amountToSwap % multiplier == 0, "not divisable by 1000000");
+       if (_amountToSwap % multiplier != 0) revert IncorrectInputAmount();

        // more code here
    
    }
}
```